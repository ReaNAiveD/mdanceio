plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.mozilla.rust-android-gradle.rust-android'
    id 'maven-publish'
    id 'signing'
}

android {
    ndkVersion rootProject.ext.build.ndkVersion
    compileSdkVersion rootProject.ext.build.compileSdkVersion
    namespace "cn.svecri.mdanceio"

    defaultConfig {
        minSdkVersion rootProject.ext.build['minSdkVersion']
        targetSdkVersion rootProject.ext.build['targetSdkVersion']

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "LIBRARY_VERSION", "\"${rootProject.ext.library.version}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility rootProject.ext.build.jvmTargetCompatibility
        targetCompatibility rootProject.ext.build.jvmTargetCompatibility
    }

    kotlinOptions {
        jvmTarget = rootProject.ext.build.jvmTargetCompatibility
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    
    sourceSets {
        test.jniLibs.srcDirs += "$buildDir/rustJniLibs/android"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
    
    libraryVariants.configureEach { variant ->
        def uniffiGeneratedPath = "generated/source/uniffi/${variant.name}/java"
        def t = tasks.register("generate${variant.name.capitalize()}UniFFIBindings", Exec) {
            workingDir project.rootDir
            commandLine 'cargo', 'run', '-p', 'mdanceio-android', '--features=uniffi/cli', '--bin', 'uniffi-bindgen', 'generate', "${project.projectDir}/../src/mdanceio.udl", '--language', 'kotlin', '--out-dir', "${buildDir}/${uniffiGeneratedPath}"
            outputs.dir "${buildDir}/${uniffiGeneratedPath}"
            // Re-generate if the interface definition changes.
            inputs.file "${project.projectDir}/../src/mdanceio.udl"
            inputs.file "${project.projectDir}/../uniffi.toml"
            // Re-generate if our uniffi-bindgen version changes.
            inputs.file "${project.rootDir}/Cargo.lock"
        }
        variant.registerJavaGeneratingTask(t.get(), new File(buildDir, uniffiGeneratedPath))
    }
}

cargo {
    module  = "../.."       // Or whatever directory contains your Cargo.toml
    libname = "mdanceio-android"          // Or whatever matches Cargo.toml's [package] name.
    targets = ["x86_64", "x86", "arm", "arm64"]  // See bellow for a longer list of options
    prebuiltToolchains = true
    profile = 'release'
    targetDirectory = '../../target'
    targetIncludes = ['libmdanceio_android.so']
    apiLevel = "${rootProject.ext.build.targetSdkVersion}" as Integer
}

afterEvaluate {
    // The `cargoBuild` task isn't available until after evaluation.
    android.libraryVariants.configureEach { variant ->
        def productFlavor = ""
        variant.productFlavors.each {
            productFlavor += "${it.name.capitalize()}"
        }
        def buildType = "${variant.buildType.name.capitalize()}"
        tasks.named("pre${buildType}Build"){
            dependsOn(tasks.named("cargoBuild"))
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "net.java.dev.jna:jna:$jna_version@aar"

    testImplementation "junit:junit:$junit_version"

    androidTestImplementation "androidx.test:runner:$androidx_test_version"
}

tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = rootProject.ext.library.groupId
            artifactId = "mdanceio-ar"
            version = rootProject.ext.library.version

            project.afterEvaluate {
                from components.release
            }

            pom {
                name = "mdanceio-ar"
                description = "MikuMikuDance compatible implementation used in ArCore context"
                url = "https://github.com/ReaNAiveD/mdanceio"
                packaging = "aar"

                license {
                    name = "MIT"
                    url = "https://github.com/ReaNAiveD/mdanceio/blob/master/LICENSE"
                }

                developers {
                    developer {
                        name = "NAiveD"
                        email = "nice-die@live.com"
                    }
                }

                scm {
                    connection = "https://github.com/ReaNAiveD/mdanceio.git"
                    developerConnection = "https://github.com/ReaNAiveD/mdanceio.git"
                    url = "https://github.com/ReaNAiveD/mdanceio"
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
        rootProject.ext["signing.keyId"],
        rootProject.ext["signing.key"],
        rootProject.ext["signing.password"],
    )
    sign publishing.publications
}